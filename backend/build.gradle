plugins {
	id 'org.springframework.boot' version '2.3.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'checkstyle'
	id 'jacoco'
	id 'org.sonarqube'  version '3.0'
}

group = 'gov.cdc.usds'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
	kickstartVersion = '8.0.0'
}
repositories {
	mavenCentral()
}

dependencies {
	// core infrastructure
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// data layer dependencies
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'com.vladmihalcea:hibernate-types-52:2.10.0'
	implementation 'org.liquibase:liquibase-core'
	runtimeOnly 'org.postgresql:postgresql'

	// UI layer dependencies
	implementation "com.graphql-java-kickstart:graphql-spring-boot-starter:${kickstartVersion}"
	implementation "com.graphql-java-kickstart:graphiql-spring-boot-starter:${kickstartVersion}"
	implementation "com.zhokhov.graphql:graphql-datetime-spring-boot-starter:3.0.0"
	implementation 'org.json:json:20201115'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'

//	Okta dependencies
	implementation 'com.okta.spring:okta-spring-boot-starter:1.4.0'


	// test/check dependencies
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation "com.graphql-java-kickstart:graphql-spring-boot-starter-test:${kickstartVersion}"
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
}

configurations {
	compileClasspath {
		resolutionStrategy.activateDependencyLocking()
	}
	runtimeClasspath {
		resolutionStrategy.activateDependencyLocking()
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "CDCgov_prime-data-input-client"
		property "sonar.organization", "cdcgov"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}
