buildscript {
    dependencies {
        classpath 'org.liquibase:liquibase-core:4.31.1'
    }
}

plugins {
    id 'org.springframework.boot' version '3.3.6'
    id 'org.liquibase.gradle' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'com.gorylenko.gradle-git-properties' version '2.4.2'
    id "com.diffplug.spotless" version "6.25.0"
}

group = 'gov.cdc.usds'
version = '0.0.1-SNAPSHOT'
java {
    sourceCompatibility = JavaVersion.VERSION_17
}

ext {
    set('springCloudVersion', "2023.0.1")
    set('oktaVersion', "13.0.2")
    set('hibernate.version', '6.6.2.Final')
}
repositories {
    mavenCentral()
}

jar {
    enabled = false
}

dependencies {
    // core infrastructure
    implementation 'org.springframework.boot:spring-boot-starter-web:3.3.7'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-actuator'
    implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure'

    // graphql
    implementation 'org.springframework.boot:spring-boot-starter-graphql:3.4.4'
    implementation 'org.springframework:spring-web:6.2.7'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    implementation 'com.graphql-java:graphql-java:23.1'
    // graphql-java-extended-validation schema directives
    implementation 'com.graphql-java:graphql-java-extended-validation:22.0'
    // graphql-java-extended-scalars
    implementation 'com.graphql-java:graphql-java-extended-scalars:22.0'


    //Feign HTTP Client
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.4'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'

    /*
     * SECURITY PINS
     *
     * All packages that must be pinned to a specific version for vulnerability mitigation should go in this section.
     * Please do NOT scatter packages elsewhere around the file. Scattered packages make it easy for pins to become
     * stale.
     *
     * DevSecOps verifies these packages at least once per month. LAST VERIFIED: 25 June 2025.
     */
    implementation 'com.squareup.okio:okio:3.10.2'
    implementation 'org.springframework:spring-core:6.2.1'
    implementation 'org.springframework.security:spring-security-core:6.4.6'
    implementation 'org.springframework.security:spring-security-crypto:6.3.9'
    implementation 'org.springframework.security:spring-security-oauth2-client:6.4.4'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:10.1.40'
    implementation 'org.apache.tomcat.embed:tomcat-embed-websocket:11.0.5'
    implementation 'org.springframework.security:spring-security-web:6.3.5'

    // non-pinned security dependencies
    implementation 'org.owasp.encoder:encoder:1.3.1'

    // data layer dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // this provides additional types for JsonB and for ListArray types in Postgres
    implementation 'io.hypersistence:hypersistence-utils-hibernate-62:3.7.6'
    implementation 'org.liquibase:liquibase-core:4.31.1'
    implementation 'org.postgresql:postgresql:42.7.7'

    // data validation
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.13.28'
    implementation 'com.smartystreets.api:smartystreets-java-sdk:3.18.4'

    // UI layer dependencies
    implementation 'org.json:json:20231013'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate6'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'


    // Okta dependencies
    implementation 'com.okta.spring:okta-spring-boot-starter:3.0.7'
    implementation "com.okta.sdk:okta-sdk-api:${oktaVersion}"
    runtimeOnly "com.okta.sdk:okta-sdk-impl:${oktaVersion}"

    // SpringSession dependencies
    runtimeOnly 'org.springframework.session:spring-session-jdbc'

    // App insights instrumentation
    implementation 'com.microsoft.azure:applicationinsights-core:3.6.1'

    // Twilio for SMS
    implementation 'com.twilio.sdk:twilio:10.1.0'

    // SendGrid for Email
    implementation 'com.sendgrid:sendgrid-java:4.10.3'

    // For local Email
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // JPA Hibernate static metamodel generation
    implementation 'org.hibernate.orm:hibernate-jpamodelgen'
    annotationProcessor 'org.hibernate.orm:hibernate-jpamodelgen'


    implementation 'com.azure:azure-storage-queue:12.19.0'

    // RandomStringUtils to generate PatientSelfRegistrationLink
    implementation 'org.apache.commons:commons-lang3'

    // Shedlock for scheduler synchronization
    implementation 'net.javacrumbs.shedlock:shedlock-core:6.2.0'
    implementation 'net.javacrumbs.shedlock:shedlock-spring:5.12.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:6.3.0'

    // test/check dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'net.ttddyy:datasource-proxy:1.10'

    // used to autoconfigure WireMock for testing
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // WireMock (only used for testing)
    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone:2.35.0"

    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:7.6.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.3'

    implementation 'commons-validator:commons-validator:1.9.0'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.79'

    // Needed for Liquibase Gradle Plugin
    liquibaseRuntime 'org.liquibase:liquibase-core:4.31.1'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

springBoot {
    buildInfo()
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath // inefficient but extremely effective
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

checkstyleTest {
    configFile = file("config/checkstyle/checkstyleTest.xml")
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn test // tests are required to run before generating the report
}

// Prefer SPRING_DATASOURCE_URL if provided, otherwise assume localhost
def dbConnectionString = System.getenv("SPRING_DATASOURCE_URL") ?: "jdbc:postgresql://127.0.0.1:${System.getenv("SR_DB_PORT") ?: 5432}/simple_report"
def isLocalDB = dbConnectionString.contains("postgresql://db") || dbConnectionString.contains("postgresql://127")
// Use "simple_report" schema for local DBs, and "public" for non-local
def defaultSchema = isLocalDB ? "simple_report" : "public"
def noPhiPassword = System.getenv("DB_PASSWORD_NO_PHI") ?: "nophi789"

liquibase {
    activities {
        main {
            driver "org.postgresql.Driver"
            changelogFile "src/main/resources/db/changelog/db.changelog-master.yaml"
            url dbConnectionString
            username "simple_report_migrations"
            password "migrations456"
            defaultSchemaName defaultSchema
            classpath "src/main/resources"
            // this shadows application.yaml: should probably be in a shared properties file
            changelogParameters([noPhiUsername: "simple_report_no_phi", noPhiPassword: noPhiPassword])
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "CDCgov_prime-data-input-client"
        property "sonar.organization", "cdcgov"
        property "sonar.host.url", "https://sonarcloud.io"
        // In order to get both the frontend and backend code to report, we need to set the sonar project to the root directory.
        property "sonar.sources", "backend/src/main/java,frontend/src,ops/services/app_functions/"
        property "sonar.exclusions", "backend/src/**/*Config*.java,frontend/src/**/*.test.*,frontend/src/stories/**/*,frontend/src/**/*.stories.tsx,frontend/src/**/*.mock.tsx,frontend/src/generated/*.tsx,ops/services/app_functions/**/*.test.*,ops/services/app_functions/**/*config*"
        property "sonar.coverage.exclusions", "frontend/src/setupTests.js,frontend/src/index.tsx"
        property "sonar.cpd.exclusions", "frontend/src/lang/*.ts"
        property "sonar.javascript.lcov.reportPaths", "frontend/coverage/lcov.info,ops/services/app_functions/report_stream_batched_publisher/functions/coverage/lcov.info"
        property "sonar.gradle.skipCompile", "false"
        property "sonar.java.binaries", "backend/build/classes/java/main"
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        targetExclude("build/generated/sources/**/*.java")
    }
}

task testDbStart(type: Exec) {
    environment "SR_DB_PORT", testDbPort
    commandLine "docker", "compose", "--env-file", "../.env.test", "-p", "simple-report-tests", "-f", "../docker-compose.yml", "up", "-d", "db"
}

task testDbStop(type: Exec) {
    commandLine "docker", "compose", "--env-file", "../.env.test", "-p", "simple-report-tests", "-f", "../docker-compose.yml", "down", "-v"
}

task testDbReset(type: Exec) {
    environment "SR_DB_PORT", testDbPort
    commandLine "db-setup/nuke-db.sh"
}

tasks.named('test') {
    // people sometimes export this to run local dev more easily: don't let it break the tests
    environment "SPRING_PROFILES_ACTIVE", ""
    // For some reason, setting this in application-default doesn't work, but it's needed
    // to prevent the Okta client from throwing an exception when we point it at WireMock,
    // which doesn't use HTTPS
    environment "OKTA_TESTING_DISABLEHTTPSCHECK", "true"
    useJUnitPlatform()
    // uncomment this to log to stdout when each test starts, so you can tell what's going on
    // testLogging {
    //   events "started", "failed"
    // }
    systemProperty "test-db-host", testDbHost
    systemProperty "test-db-port", testDbPort
    systemProperty "skip-db-setup", skipDbSetup
    if (skipDbSetup == "false" && System.getenv("CI") == null) {
        dependsOn testDbStart
        finalizedBy testDbStop
    }
    maxHeapSize = "1024m"
}
