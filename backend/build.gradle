plugins {
    id 'org.springframework.boot' version '2.7.13'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id "com.diffplug.spotless" version "6.19.0"
}

group = 'gov.cdc.usds'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

ext {
    set('springCloudVersion', "2021.0.3")
}
repositories {
    mavenCentral()
}

jar {
    enabled = false
}

dependencies {
    // core infrastructure
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-actuator'
    implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure'

    // graphql
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'org.springframework.graphql:spring-graphql-test'

    //Feign HTTP Client
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'

    /*
     * SECURITY PINS
     *
     * All packages that must be pinned to a specific version for vulnerability mitigation should go in this section.
     * Please do NOT scatter packages elsewhere around the file. Scattered packages make it easy for pins to become
     * stale.
     *
     * DevSecOps verifies these packages at least once per month. LAST VERIFIED: 4 August 2023.
     */
    implementation 'org.yaml:snakeyaml:1.33' //Verified 4 Aug 2023. Removal restores snakeyaml to 1.30, which is vulnerable to DoS.
    implementation 'org.springframework.security:spring-security-config:5.7.10' //Verified 4 Aug 2023. Surrounding versions are vulnerable to myriad issues.
    implementation 'org.thymeleaf:thymeleaf:3.1.2.RELEASE' //Verified 4 Aug 2023. Resolves critical vulnerability caused by org.springframework.boot plugin @ 2.7.13.

    // data layer dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.vladmihalcea:hibernate-types-55:2.19.2'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'

    // data validation
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.13.14'
    implementation 'com.smartystreets.api:smartystreets-java-sdk:3.13.15'

    // UI layer dependencies
    implementation 'org.json:json:20230618'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Okta dependencies
    implementation 'com.okta.spring:okta-spring-boot-starter:2.1.7'
    implementation 'com.okta.sdk:okta-sdk-api:12.0.2'
    runtimeOnly 'com.okta.sdk:okta-sdk-impl:12.0.2'
    // Spring dependency management pulls 5.1.4, but okta 12.0.2 requires 5.2+ for the new
    // This can be removed in the springboot 3 upgrade
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'

    // SpringSession dependencies
    runtimeOnly 'org.springframework.session:spring-session-jdbc'

    // App insights instrumentation
    implementation 'com.microsoft.azure:applicationinsights-core:3.4.15'

    // Twilio for SMS
    implementation group: "com.twilio.sdk", name: "twilio", version: "9.9.1"

    // SendGrid for Email
    implementation 'com.sendgrid:sendgrid-java:4.9.3'

    // For local Email
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // JPA Hibernate static metamodel generation
    implementation 'org.hibernate.orm:hibernate-jpamodelgen:6.2.7.Final'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'

    // graphql-java-extended-validation schema directives
    implementation 'com.graphql-java:graphql-java-extended-validation:18.1-hibernate-validator-6.2.0.Final'
    // graphql-java-extended-scalars
    implementation 'com.graphql-java:graphql-java-extended-scalars:18.1'

    implementation 'com.azure:azure-storage-queue:12.17.1'

    // RandomStringUtils to generate PatientSelfRegistrationLink
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // Shedlock for scheduler synchronization
    implementation 'net.javacrumbs.shedlock:shedlock-core:4.42.0'
    implementation 'net.javacrumbs.shedlock:shedlock-spring:4.42.0'
    implementation 'net.javacrumbs.shedlock:shedlock-provider-jdbc-template:4.42.0'

    // test/check dependencies
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.yannbriancon:spring-hibernate-query-utils:2.0.0'

    // used to autoconfigure WireMock for testing
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // WireMock (only used for testing)
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.35.0"

    implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:6.6.2'

    implementation 'commons-validator:commons-validator:1.7'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

springBoot {
    buildInfo()
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath // inefficient but extremely effective
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

checkstyleTest {
    configFile = file("config/checkstyle/checkstyleTest.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
    dependsOn test // tests are required to run before generating the report
}

// Prefer SPRING_DATASOURCE_URL if provided, otherwise assume localhost
def dbConnectionString = System.getenv("SPRING_DATASOURCE_URL") ?: "jdbc:postgresql://127.0.0.1:${System.getenv("SR_DB_PORT") ?: 5432}/simple_report"
def isLocalDB = dbConnectionString.contains("postgresql://db") || dbConnectionString.contains("postgresql://127")
// Use "simple_report" schema for local DBs, and "public" for non-local
def defaultSchema = isLocalDB ? "simple_report" : "public"
def noPhiPassword = System.getenv("DB_PASSWORD_NO_PHI") ?: "nophi789"

liquibase {
    activities {
        main {
            driver "org.postgresql.Driver"
            changeLogFile "/db/changelog/db.changelog-master.yaml"
            url dbConnectionString
            username "simple_report_migrations"
            password "migrations456"
            defaultSchemaName defaultSchema
            classpath "src/main/resources"
            // this shadows application.yaml: should probably be in a shared properties file
            changeLogParameters([noPhiUsername: "simple_report_no_phi", noPhiPassword: noPhiPassword])
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "CDCgov_prime-data-input-client"
        property "sonar.organization", "cdcgov"
        property "sonar.host.url", "https://sonarcloud.io"
        // In order to get both the frontend and backend code to report, we need to set the sonar project to the root directory.
        property "sonar.sources", "backend/src/main/java,frontend/src,ops/services/app_functions/"
        property "sonar.exclusions", "backend/src/**/*Config*.java,frontend/src/**/*.test.*,frontend/src/stories/**/*,frontend/src/**/*.stories.tsx,frontend/src/**/*.mock.tsx,frontend/src/generated/*.tsx,ops/services/app_functions/**/*.test.*,ops/services/app_functions/**/*config*"
        property "sonar.coverage.exclusions", "frontend/src/setupTests.js,frontend/src/index.tsx"
        property "sonar.cpd.exclusions", "frontend/src/lang/*.ts"
        property "sonar.javascript.lcov.reportPaths", "frontend/coverage/lcov.info,ops/services/app_functions/report_stream_batched_publisher/functions/coverage/lcov.info"
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        targetExclude("build/generated/sources/**/*.java")
    }
}

task testDbStart(type: Exec) {
    environment "SR_DB_PORT", testDbPort
    commandLine "docker", "compose", "--env-file", "../.env.test", "-p", "simple-report-tests", "-f", "../docker-compose.yml", "up", "-d", "db"
}

task testDbStop(type: Exec) {
    commandLine "docker", "compose", "--env-file", "../.env.test", "-p", "simple-report-tests", "-f", "../docker-compose.yml", "down", "-v"
}

task testDbReset(type: Exec) {
    environment "SR_DB_PORT", testDbPort
    commandLine "db-setup/nuke-db.sh"
}

tasks.named('test') {
    // people sometimes export this to run local dev more easily: don't let it break the tests
    environment "SPRING_PROFILES_ACTIVE", ""
    // For some reason, setting this in application-default doesn't work, but it's needed
    // to prevent the Okta client from throwing an exception when we point it at WireMock,
    // which doesn't use HTTPS
    environment "OKTA_TESTING_DISABLEHTTPSCHECK", "true"
    useJUnitPlatform()
    // uncomment this to log to stdout when each test starts, so you can tell what's going on
    // testLogging {
    //   events "started", "failed"
    // }
    systemProperty "test-db-host", testDbHost
    systemProperty "test-db-port", testDbPort
    systemProperty "skip-db-setup", skipDbSetup
    if (skipDbSetup == "false" && System.getenv("CI") == null) {
        dependsOn testDbStart
        finalizedBy testDbStop
    }
    maxHeapSize = "1024m"
}
