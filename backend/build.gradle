plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube'  version '3.0'
    id 'com.gorylenko.gradle-git-properties' version '2.2.0'
    id "com.diffplug.spotless" version "5.14.0"
}

group = 'gov.cdc.usds'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
    kickstartVersion = '11.0.0'
    set('springCloudVersion', "Hoxton.SR11")
}
repositories {
    mavenCentral()
}

dependencies {
    // core infrastructure
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // data layer dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.vladmihalcea:hibernate-types-52:2.10.0'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'

    // data validation
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.12.15'
    implementation 'com.smartystreets.api:smartystreets-java-sdk:3.7.2'

    // UI layer dependencies
    implementation "com.graphql-java-kickstart:graphql-spring-boot-starter:${kickstartVersion}"
    implementation "com.graphql-java-kickstart:graphiql-spring-boot-starter:${kickstartVersion}"
    implementation "com.zhokhov.graphql:graphql-datetime-spring-boot-starter:4.0.0"
    implementation 'org.json:json:20201115'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Okta dependencies
    implementation 'com.okta.spring:okta-spring-boot-starter:2.0.0'
    implementation 'com.okta.sdk:okta-sdk-api:3.0.2'
    runtimeOnly 'com.okta.sdk:okta-sdk-impl:3.0.2'
    runtimeOnly 'com.okta.sdk:okta-sdk-httpclient:3.0.2'

    // SpringSession dependencies
    runtimeOnly 'org.springframework.session:spring-session-jdbc'

    // App insights instrumentation
    implementation 'com.microsoft.azure:applicationinsights-core:2.6.3'

    // Azure authentication library (for Dynamics 365)
    implementation 'com.microsoft.azure:adal4j:1.6.7'

    // Twilio for SMS
    implementation group: "com.twilio.sdk", name: "twilio", version: "8.7.0"

    // SendGrid for Email
    implementation 'com.sendgrid:sendgrid-java:4.7.2'

    // JPA Hibernate static metamodel generation
    compile 'org.hibernate:hibernate-jpamodelgen:5.4.27.Final'
    annotationProcessor'org.hibernate:hibernate-jpamodelgen'

    // graphql-java-extended-validation schema directives
    compile 'com.graphql-java:graphql-java-extended-validation:16.0.0'

    implementation 'com.azure:azure-storage-queue:12.8.+'

    // test/check dependencies
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "com.graphql-java-kickstart:graphql-spring-boot-starter-test:${kickstartVersion}"
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.yannbriancon:spring-hibernate-query-utils:1.0.3'

    // used to autoconfigure WireMock for testing
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    // WireMock (only used for testing)
    testCompile "com.github.tomakehurst:wiremock-jre8:2.28.1"

    // Forcing these version resolutions over the BOM of spring-cloud-dependencies below
    runtimeOnly "com.azure:azure-core-http-netty:1.9.2"
    runtimeOnly "com.azure:azure-core:1.14.1"
    runtimeOnly "com.azure:azure-storage-common:12.11.1"
    runtimeOnly "com.azure:azure-storage-queue:12.9.1"
    runtimeOnly "io.zipkin.brave:brave-instrumentation-http:5.13.2"
    runtimeOnly "io.zipkin.brave:brave:5.13.2"
    runtimeOnly "io.zipkin.reporter2:zipkin-reporter-brave:2.16.3"
    runtimeOnly "io.zipkin.reporter2:zipkin-reporter:2.16.1"
    runtimeOnly "io.zipkin.zipkin2:zipkin:2.23.0"

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

springBoot {
    buildInfo()
}

test {
    // people sometimes export this to run local dev more easily: don't let it break the tests
    environment "SPRING_PROFILES_ACTIVE", ""
    // For some reason, setting this in application-default doesn't work, but it's needed
    // to prevent the Okta client from throwing an exception when we point it at WireMock,
    // which doesn't use HTTPS
    environment "OKTA_TESTING_DISABLEHTTPSCHECK", "true"
    useJUnitPlatform()
    // uncomment this to log to stdout when each test starts, so you can tell what's going on
    // testLogging {
    //   events "started", "failed"
    // }
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath // inefficient but extremely effective
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

checkstyleTest {
   configFile = file("config/checkstyle/checkstyleTest.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
    dependsOn test // tests are required to run before generating the report
}

liquibase {
    activities {
        main {
            driver "org.postgresql.Driver"
            changeLogFile "/db/changelog/db.changelog-master.yaml"
            url "jdbc:postgresql://127.0.0.1:${System.getenv("SR_DB_PORT") ?: 5432}/simple_report"
            username "postgres"
            password "admin_password_for_local_dev_is_not_very_secure"
            defaultSchemaName "simple_report"
            classpath "src/main/resources"
            // this shadows application.yaml: should probably be in a shared properties file
            changeLogParameters noPhiUsername: "simple_report_no_phi"
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "CDCgov_prime-data-input-client"
        property "sonar.organization", "cdcgov"
        property "sonar.host.url", "https://sonarcloud.io"
        // In order to get both the frontend and backend code to report, we need to set the sonar project to the root directory.
        property "sonar.sources", "backend/src/main/java,frontend/src"
        property "sonar.exclusions", "frontend/src/**/*.test.*,frontend/src/stories/**/*,frontend/src/**/*.stories.tsx"
        property "sonar.cpd.exclusions", "frontend/src/lang/*.ts"
        property "sonar.javascript.lcov.reportPaths", "frontend/coverage/lcov.info"
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}

task testDbStart(type: Exec) {
    environment "SR_DB_PORT", testDbPort
    commandLine "docker-compose", "-p", "simple-report-tests", "up", "-d", "db"
}

task testDbStop(type: Exec) {
    commandLine "docker-compose", "-p", "simple-report-tests", "down"
}

task testDbReset(type: Exec) {
    environment "SR_DB_PORT", testDbPort
    commandLine "db-setup/nuke-db.sh"
}

test {
    systemProperty "test-db-port", testDbPort
    if (System.getenv("CI") == null) {
        dependsOn testDbStart
    }
}
