plugins {
    id 'org.springframework.boot' version '2.7.4'
    id 'org.liquibase.gradle' version '2.0.4'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version '3.0'
    id 'com.gorylenko.gradle-git-properties' version '2.4.0'
    id "com.diffplug.spotless" version "5.14.0"
}

group = 'gov.cdc.usds'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
    set('springCloudVersion', "2021.0.3")
}
repositories {
    mavenCentral()
}

jar {
    enabled = false
}

dependencies {
    // core infrastructure
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-actuator'
    implementation 'org.springframework.boot:spring-boot-actuator-autoconfigure'

    // graphql
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'org.springframework.graphql:spring-graphql-test'

    //Feign HTTP Client
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'

    // log4j vulnerability fixes (Dec 2021)
    implementation 'org.apache.logging.log4j:log4j-api:2.17.0'
    implementation 'org.apache.logging.log4j:log4j-to-slf4j:2.17.0'

    // Security Pins (Sept 2022)
    implementation 'org.yaml:snakeyaml:1.31'

    // data layer dependencies
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.vladmihalcea:hibernate-types-55:2.19.2'
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql:42.4.1'

    // data validation
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.12.57'
    implementation 'com.smartystreets.api:smartystreets-java-sdk:3.13.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    // UI layer dependencies
    implementation 'org.json:json:20201115'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Okta dependencies
    implementation 'com.okta.spring:okta-spring-boot-starter:2.1.6'
    implementation 'com.okta.sdk:okta-sdk-api:8.2.1'
    implementation 'org.springframework.security:spring-security-web:5.7.5' //TODO: Remove when Okta's Spring Boot Starter version increases beyond 2.1.6.
    implementation 'org.springframework.security:spring-security-oauth2-client:5.7.5' //TODO: Remove when Okta's Spring Boot Starter version increases beyond 2.1.6.
    runtimeOnly 'com.okta.sdk:okta-sdk-impl:8.2.1'
    runtimeOnly 'com.okta.sdk:okta-sdk-httpclient:8.2.1'

    // SpringSession dependencies
    runtimeOnly 'org.springframework.session:spring-session-jdbc'

    // App insights instrumentation
    implementation 'com.microsoft.azure:applicationinsights-core:2.6.3'

    // Twilio for SMS
    implementation group: "com.twilio.sdk", name: "twilio", version: "8.7.0"

    // SendGrid for Email
    implementation 'com.sendgrid:sendgrid-java:4.7.2'

    // For local Email
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // JPA Hibernate static metamodel generation
    implementation 'org.hibernate:hibernate-jpamodelgen:5.4.27.Final'
    annotationProcessor 'org.hibernate:hibernate-jpamodelgen'

    // graphql-java-extended-validation schema directives
    implementation 'com.graphql-java:graphql-java-extended-validation:18.1-hibernate-validator-6.2.0.Final'
    // graphql-java-extended-scalars
    implementation 'com.graphql-java:graphql-java-extended-scalars:18.1'

    implementation 'com.azure:azure-storage-queue:12.15.+'
    implementation 'com.azure:azure-core-http-netty:1.11.9'
    implementation 'com.azure:azure-core:1.27.0'
    implementation 'com.azure:azure-storage-common:12.15.1'

    // RandomStringUtils to generate PatientSelfRegistrationLink
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // test/check dependencies
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.yannbriancon:spring-hibernate-query-utils:1.0.3'

    // used to autoconfigure WireMock for testing
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-stub-runner'
    testImplementation 'org.awaitility:awaitility:4.1.0'

    // dev tools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // WireMock (only used for testing)
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.31.0"

    runtimeOnly "io.zipkin.brave:brave-instrumentation-http:5.13.2"
    runtimeOnly "io.zipkin.brave:brave:5.13.2"
    runtimeOnly "io.zipkin.reporter2:zipkin-reporter-brave:2.16.3"
    runtimeOnly "io.zipkin.reporter2:zipkin-reporter:2.16.1"
    runtimeOnly "io.zipkin.zipkin2:zipkin:2.23.0"

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

springBoot {
    buildInfo()
}

configurations {
    liquibaseRuntime.extendsFrom runtimeClasspath // inefficient but extremely effective
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

checkstyleTest {
    configFile = file("config/checkstyle/checkstyleTest.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
    dependsOn test // tests are required to run before generating the report
}

// Prefer SPRING_DATASOURCE_URL if provided, otherwise assume localhost
def dbConnectionString = System.getenv("SPRING_DATASOURCE_URL") ?: "jdbc:postgresql://127.0.0.1:${System.getenv("SR_DB_PORT") ?: 5432}/simple_report"
def isLocalDB = dbConnectionString.contains("postgresql://db") || dbConnectionString.contains("postgresql://127")
// Use "simple_report" schema for local DBs, and "public" for non-local
def defaultSchema = isLocalDB ? "simple_report" : "public"
def noPhiPassword = System.getenv("DB_PASSWORD_NO_PHI") ?: "nophi789"

liquibase {
    activities {
        main {
            driver "org.postgresql.Driver"
            changeLogFile "/db/changelog/db.changelog-master.yaml"
            url dbConnectionString
            username "simple_report_migrations"
            password "migrations456"
            defaultSchemaName defaultSchema
            classpath "src/main/resources"
            // this shadows application.yaml: should probably be in a shared properties file
            changeLogParameters([noPhiUsername: "simple_report_no_phi", noPhiPassword: noPhiPassword])
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "CDCgov_prime-data-input-client"
        property "sonar.organization", "cdcgov"
        property "sonar.host.url", "https://sonarcloud.io"
        // In order to get both the frontend and backend code to report, we need to set the sonar project to the root directory.
        property "sonar.sources", "backend/src/main/java,frontend/src,ops/services/app_functions/"
        property "sonar.exclusions", "backend/src/**/*Config*.java,frontend/src/**/*.test.*,frontend/src/stories/**/*,frontend/src/**/*.stories.tsx,frontend/src/**/*.mock.tsx,frontend/src/generated/*.tsx,ops/services/app_functions/**/*.test.*,ops/services/app_functions/**/*config*"
        property "sonar.coverage.exclusions", "frontend/src/setupTests.js"
        property "sonar.cpd.exclusions", "frontend/src/lang/*.ts"
        property "sonar.javascript.lcov.reportPaths", "frontend/coverage/lcov.info,ops/services/app_functions/report_stream_batched_publisher/functions/coverage/lcov.info"
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        targetExclude("build/generated/sources/**/*.java")
    }
}

task testDbStart(type: Exec) {
    environment "SR_DB_PORT", testDbPort
    commandLine "docker", "compose", "--env-file", "../.env.test", "-p", "simple-report-tests", "-f", "../docker-compose.yml", "up", "-d", "db"
}

task testDbStop(type: Exec) {
    commandLine "docker", "compose", "--env-file", "../.env.test", "-p", "simple-report-tests", "-f", "../docker-compose.yml", "down", "-v"
}

task testDbReset(type: Exec) {
    environment "SR_DB_PORT", testDbPort
    commandLine "db-setup/nuke-db.sh"
}

tasks.named('test') {
    // people sometimes export this to run local dev more easily: don't let it break the tests
    environment "SPRING_PROFILES_ACTIVE", ""
    // For some reason, setting this in application-default doesn't work, but it's needed
    // to prevent the Okta client from throwing an exception when we point it at WireMock,
    // which doesn't use HTTPS
    environment "OKTA_TESTING_DISABLEHTTPSCHECK", "true"
    useJUnitPlatform()
    // uncomment this to log to stdout when each test starts, so you can tell what's going on
    // testLogging {
    //   events "started", "failed"
    // }
    systemProperty "test-db-host", testDbHost
    systemProperty "test-db-port", testDbPort
    systemProperty "skip-db-setup", skipDbSetup
    if (skipDbSetup == "false" && System.getenv("CI") == null) {
        dependsOn testDbStart
        finalizedBy testDbStop
    }
}

